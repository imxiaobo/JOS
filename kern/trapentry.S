/* See COPYRIGHT for copyright information. */

#include <inc/mmu.h>
#include <inc/memlayout.h>
#include <inc/trap.h>



###################################################################
# exceptions/interrupts
###################################################################

/* The TRAPHANDLER macro defines a globally-visible function for handling
 * a trap.  It pushes a trap number onto the stack, then jumps to _alltraps.
 * Use TRAPHANDLER for traps where the CPU automatically pushes an error code.
 */ 
#define TRAPHANDLER(name, num)						\
	.globl name;		/* define global symbol for 'name' */	\
	.type name, @function;	/* symbol type is function */		\
	.align 2;		/* align function definition */		\
	name:			/* function starts here */		\
	pushl $(num);							\
	jmp _alltraps

/* Use TRAPHANDLER_NOEC for traps where the CPU doesn't push an error code.
 * It pushes a 0 in place of the error code, so the trap frame has the same
 * format in either case.
 */
#define TRAPHANDLER_NOEC(name, num)					\
	.globl name;							\
	.type name, @function;						\
	.align 2;							\
	name:								\
	pushl $0;							\
	pushl $(num);							\
	jmp _alltraps

.text

/*
 * Lab 3: Your code here for generating entry points for the different traps.
 */

	TRAPHANDLER_NOEC(divide_error, T_DIVIDE)				# divide error
	TRAPHANDLER_NOEC(debug, T_DEBUG)						# debug exception
	TRAPHANDLER_NOEC(nmi, T_NMI)							# non-maskable interrupt
	TRAPHANDLER_NOEC(breakpoint, T_BRKPT)					# breakpoint
	TRAPHANDLER_NOEC(overflow, T_OFLOW)						# overflow
	TRAPHANDLER_NOEC(bounds, T_BOUND)						# bounds check
	TRAPHANDLER_NOEC(invalid_op, T_ILLOP)					# illegal opcode
	TRAPHANDLER_NOEC(device_not_available, T_DEVICE)		# device not available
	TRAPHANDLER(double_fault, T_DBLFLT)						# double fault
//	TRAPHANDLER_NOEC(coprocessor_segment_overrun, T_COPROC)	# reserved (not generated by recent processors)
	TRAPHANDLER(invalid_TSS, T_TSS)							# invalid task switch segment
	TRAPHANDLER(segment_not_present, T_SEGNP)				# segment not present
	TRAPHANDLER(stack_segment, T_STACK)						# stack exception
	TRAPHANDLER(general_protection, T_GPFLT)				# general protection fault
	TRAPHANDLER(page_fault, T_PGFLT)						# page fault
//	TRAPHANDLER_NOEC(reserved, T_RES)						# reserved
	TRAPHANDLER_NOEC(float_point_error, T_FPERR)			# floating point error
	TRAPHANDLER(alignment_check, T_ALIGN)					# alignment check
	TRAPHANDLER_NOEC(machine_check, T_MCHK)					# machine check
	TRAPHANDLER_NOEC(SIMD_float_point_error, T_SIMDERR)		# SIMD floating point error

	TRAPHANDLER_NOEC(system_call, T_SYSCALL)				# system call


	TRAPHANDLER_NOEC(trap_default, T_DEFAULT)



/*
 * Lab 3: Your code here for _alltraps
 */

	_alltraps:
	    # Build trap frame.
	    pushl %ds
	    pushl %es
	    pushal
	    # Set up data segments.
	    movl $GD_KD, %eax
	    movw %ax,%ds
	    movw %ax,%es
	    # Call trap(tf), where tf=%esp
	    pushl %esp
	    call trap
	    popl %esp

	    # Cleanup pushes and ret
	    popal
	    popl %es
	    popl %ds
	    iret

